;;; arxana-store-test.el --- Tests for Futon storage bridge -*- lexical-binding: t; -*-

(require 'ert)
(require 'arxana-store)

(defvar futon4-enable-sync t)
(defvar futon4-base-url "http://example")

(ert-deftest arxana-store-ensure-article-derives-id ()
  (let ((futon4-enable-sync t)
        (calls '()))
    (cl-letf (((symbol-function 'futon4--canonical-path)
               (lambda (path) (concat "canon:" path)))
              ((symbol-function 'futon4--article-id-for)
               (lambda (_name path) (concat "id:" path)))
              ((symbol-function 'futon4-ensure-article-entity)
               (lambda (id name path &optional spine _cb props)
                 (let* ((combined (delq nil (append props
                                                    (list (when path (cons 'path path))
                                                          (when spine (cons 'spine t))))))
                        (payload (delq nil (list (cons 'id id)
                                                 (cons 'name name)
                                                 (cons 'type "arxana/article")
                                                 (when combined (cons 'props combined))))))
                   (push payload calls)))))
      (should (string= "id:canon:Test"
                       (arxana-store-ensure-article :name "Test" :path "Test")))
      (let ((payload (car calls)))
        (should (equal (cdr (assoc 'name payload)) "Test"))
        (should (assoc 'props payload))))))

(ert-deftest arxana-store-ensure-article-disabled ()
  (let ((futon4-enable-sync nil))
    (should-not (arxana-store-ensure-article :name "Test"))
    (should (eq (plist-get arxana-store-last-error :reason) 'disabled))))

(ert-deftest arxana-store-request-parses-json ()
  (let ((futon4-enable-sync t)
        (futon4-base-url "http://example"))
    (cl-letf (((symbol-function 'url-retrieve-synchronously)
               (lambda (&rest _)
                 (let ((buf (generate-new-buffer " *arxana-store-test*")))
                   (with-current-buffer buf
                     (insert "HTTP/1.1 200 OK\r\n"
                             "Content-Type: application/json\r\n\r\n"
                             "{\"entity\":{\"id\":\"one\"}}")
                     (goto-char (point-min)))
                   buf))))
      (let ((body (arxana-store-fetch-entity "arxana:article:test")))
        (should (equal '((:entity (:id . "one"))) body))))))

(ert-deftest arxana-store-ensure-article-props ()
  (let ((futon4-enable-sync t)
        (calls '()))
    (cl-letf (((symbol-function 'futon4--canonical-path)
               (lambda (path) path))
              ((symbol-function 'futon4--article-id-for)
               (lambda (&rest _) "arxana:article:test"))
              ((symbol-function 'futon4-ensure-article-entity)
               (lambda (id name path &optional spine _cb props)
                 (let* ((combined (delq nil (append props
                                                    (list (when path (cons 'path path))
                                                          (when spine (cons 'spine t))))))
                        (payload (delq nil (list (cons 'id id)
                                                 (cons 'name name)
                                                 (cons 'type "arxana/article")
                                                 (when combined (cons 'props combined))))))
                   (push payload calls)))))
      (arxana-store-ensure-article :name "Test" :props '((status . "deleted")))
      (let* ((payload (car calls))
             (props (cdr (assoc 'props payload))))
        (should (equal (assoc 'status props) '(status . "deleted")))))))

(ert-deftest arxana-store-request-records-downtime ()
  (let ((futon4-enable-sync t)
        (futon4-base-url "http://example"))
    (cl-letf (((symbol-function 'url-retrieve-synchronously)
               (lambda (&rest _) nil)))
      (should-not (arxana-store-fetch-entity "foo"))
      (should (eq (plist-get arxana-store-last-error :reason) 'connection)))))

(ert-deftest arxana-store-upsert-scholium-builds-ids ()
  (let ((futon4-enable-sync t)
        (calls nil))
    (cl-letf (((symbol-function 'futon4--article-id-for)
               (let ((counter 0))
                 (lambda (name &optional _path)
                   (setq counter (1+ counter))
                   (format "id-%s" name))))
              ((symbol-function 'arxana-store--post-relation)
               (lambda (src dst label &optional _extra)
                 (push (list src dst label) calls)
                 '((:ok . t)))))
      (should (equal '(:src "id-Source" :dst "id-Target" :label "note")
                     (arxana-store-upsert-scholium "Source" "Target" "note")))
      (should (equal (list (list "id-Source" "id-Target" "note")) calls)))))

(ert-deftest arxana-store-upsert-scholium-fails-without-response ()
  (let ((futon4-enable-sync t))
    (cl-letf (((symbol-function 'futon4--article-id-for)
               (lambda (&rest _) "id"))
              ((symbol-function 'arxana-store--post-relation)
               (lambda (&rest _) nil)))
      (should-not (arxana-store-upsert-scholium "Source" "Target")))))

(ert-deftest arxana-store-post-relation-builds-payload ()
  (let ((futon4-enable-sync t)
        (captured nil))
    (cl-letf (((symbol-function 'arxana-store--request)
               (lambda (method path payload &optional _query)
                 (setq captured (list method path payload))
                 '((:relation . t)))))
      (should (equal '((:relation . t))
                     (arxana-store--post-relation "src" "dst" "label" '((tag . t)))))
      (should (equal captured
                     (list "POST" "/relation"
                           '((type . "arxana/scholium")
                             (src . "src")
                             (dst . "dst")
                             (props . ((label . "label") (tag . t))))))))))

(ert-deftest arxana-store-post-relation-validates-inputs ()
  (let ((futon4-enable-sync t))
    (should-not (arxana-store--post-relation nil "dst"))
    (should (eq (plist-get arxana-store-last-error :reason) 'invalid))))

(ert-deftest arxana-store-post-relation-disabled ()
  (let ((futon4-enable-sync nil))
    (should-not (arxana-store--post-relation "src" "dst"))
    (should (eq (plist-get arxana-store-last-error :reason) 'disabled))))

(ert-deftest arxana-store-post-hyperedge-builds-payload ()
  (let ((futon4-enable-sync t)
        (captured nil))
    (cl-letf (((symbol-function 'arxana-store--request)
               (lambda (method path payload &optional query)
                 (setq captured (list method path payload query))
                 '((:hyperedge . t)))))
      (let* ((endpoints '(((role . ":role/source") (entity . "src"))
                          ((role . ":role/target") (entity . "dst"))))
             (response (arxana-store--post-hyperedge "arxana/derivation"
                                                     ":hx/derives"
                                                     endpoints
                                                     '((label . "passage")))))
        (should (equal '((:hyperedge . t)) response))
        (should (equal captured
                       (list "POST" "/hyperedge"
                             '((type . "arxana/derivation")
                               (hx/type . ":hx/derives")
                               (hx/endpoints . (((role . ":role/source") (entity . "src"))
                                                ((role . ":role/target") (entity . "dst"))))
                               (props . ((label . "passage"))))
                             nil)))))))

(ert-deftest arxana-store-post-hyperedge-validates-inputs ()
  (let ((futon4-enable-sync t))
    (should-not (arxana-store--post-hyperedge "arxana/derivation" nil '((foo . t))))
    (should (eq (plist-get arxana-store-last-error :reason) 'invalid))
    (should-not (arxana-store--post-hyperedge "arxana/derivation" ":hx/derives" nil))
    (should (eq (plist-get arxana-store-last-error :reason) 'invalid))))

(ert-deftest arxana-store-post-hyperedge-disabled ()
  (let ((futon4-enable-sync nil))
    (should-not (arxana-store--post-hyperedge "arxana/derivation" ":hx/derives" '((e . t))))
    (should (eq (plist-get arxana-store-last-error :reason) 'disabled))))

(ert-deftest arxana-store-save-snapshot-builds-request ()
  (let ((futon4-enable-sync t)
        (captured nil))
    (cl-letf (((symbol-function 'arxana-store--request)
               (lambda (method path payload &optional _query)
                 (setq captured (list method path payload))
                 '((:snapshot . t)))))
      (should (equal '((:snapshot . t))
                     (arxana-store-save-snapshot 'all "manual")))
      (should (equal captured
                     (list "POST" "/snapshot"
                           '((scope . "all") (label . "manual"))))))))

(ert-deftest arxana-store-save-snapshot-disabled ()
  (let ((futon4-enable-sync nil))
    (should-not (arxana-store-save-snapshot 'latest))
    (should (eq (plist-get arxana-store-last-error :reason) 'disabled))))

(ert-deftest arxana-store-save-snapshot-validates-scope ()
  (let ((futon4-enable-sync t))
    (should-not (arxana-store-save-snapshot 'unknown))
    (should (eq (plist-get arxana-store-last-error :reason) 'invalid))))

(ert-deftest arxana-store-restore-snapshot-builds-request ()
  (let ((futon4-enable-sync t)
        (captured nil))
    (cl-letf (((symbol-function 'arxana-store--request)
               (lambda (method path payload &optional _query)
                 (setq captured (list method path payload))
                 '((:restored . t)))))
      (should (equal '((:restored . t))
                     (arxana-store-restore-snapshot "snap-1" 'latest)))
      (should (equal captured
                     (list "POST" "/snapshot/restore"
                           '((action . "restore") (scope . "latest")
                             (snapshot/id . "snap-1"))))))))

(ert-deftest arxana-store-restore-snapshot-disabled ()
  (let ((futon4-enable-sync nil))
    (should-not (arxana-store-restore-snapshot "snap" 'all))
    (should (eq (plist-get arxana-store-last-error :reason) 'disabled))))

(ert-deftest arxana-store-nema-shim-delegates ()
  (let ((futon4-enable-sync t)
        (calls nil)
        (callback-arg nil))
    (cl-letf (((symbol-function 'arxana-store--post-relation)
               (lambda (&rest args)
                 (push args calls)
                 '((:ok . t)))))
      (arxana-store--nema-simple-wrapper "src" "dst" "lbl"
                                         (lambda (body) (setq callback-arg body)))
      (should (equal calls '(("src" "dst" "lbl"))))
      (should (equal callback-arg '((:ok . t)))))))

(ert-deftest arxana-store-hyperedge-shim-delegates ()
  (let ((futon4-enable-sync t)
        (calls nil)
        (callback-arg nil))
    (cl-letf (((symbol-function 'arxana-store--post-hyperedge)
               (lambda (&rest args)
                 (push args calls)
                 '((:edge . t)))))
      (arxana-store--hyperedge-wrapper "arxana/derivation" ":hx/derives" '((foo)) '((label . "L"))
                                       (lambda (body) (setq callback-arg body)))
      (should (equal calls '(("arxana/derivation" ":hx/derives" ((foo)) ((label . "L"))))))
      (should (equal callback-arg '((:edge . t)))))))

(ert-deftest arxana-store-cooccur-builds-request ()
  (let ((futon4-enable-sync t)
        (captured nil))
    (cl-letf (((symbol-function 'arxana-store--request)
               (lambda (method path payload query)
                 (setq captured (list method path payload query))
                 '((:cooccur . t)))))
      (should (equal '((:cooccur . t))
                     (arxana-store-cooccur "Demo" 7)))
      (should (equal captured
                     (list "GET" "/cooccur/Demo" nil "limit=7"))))))

(ert-deftest arxana-store-cooccur-validates-name ()
  (let ((futon4-enable-sync t))
    (should-not (arxana-store-cooccur ""))
    (should (eq (plist-get arxana-store-last-error :reason) 'invalid))))

(ert-deftest arxana-store-tail-builds-request ()
  (let ((futon4-enable-sync t)
        (captured nil))
    (cl-letf (((symbol-function 'arxana-store--request)
               (lambda (method path payload query)
                 (setq captured (list method path payload query))
                 '((:tail . t)))))
      (should (equal '((:tail . t))
                     (arxana-store-tail 3)))
      (should (equal captured
                     (list "GET" "/tail" nil "limit=3"))))))

(provide 'arxana-store-test)
;;; arxana-store-test.el ends here
