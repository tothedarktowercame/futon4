;;; arxana-adjacency-test.el --- Tests for scholium adjacency syncing -*- lexical-binding: t; -*-

(require 'ert)
(require 'arxana-store)

(add-to-list 'load-path (expand-file-name "arxana/dev" default-directory))

(let ((tangled (expand-file-name "arxana/arxana-tangled.el" default-directory)))
  (unless (featurep 'arxana-tangled)
    (load-file tangled)))

(arxana-store--install-relation-shim)

(ignore-errors (require 'arxana-article))

(ert-deftest arxana-adjacency-syncs-hyperedges ()
  (let ((futon4-enable-sync t)
        (captured nil)
        (relation-called nil))
    (cl-letf (((symbol-function 'futon4--article-id-for)
               (lambda (name &optional _path)
                 (pcase name
                   ("Source" "id-source")
                   ("Target" "id-target")
                   ("Transcribed" "id-transcribed"))))
              ((symbol-function 'futon4-lookup-article-id)
               (lambda (name)
                 (when (string= name "Source") "id-source")))
              ((symbol-function 'futon4-store-hyperedge)
               (lambda (type hx endpoints &optional props _cb)
                 (setq captured (list type hx endpoints props))
                 '((:edge . t))))
              ((symbol-function 'futon4-store-nema-simple)
               (lambda (&rest _)
                 (setq relation-called t))))
      (futon4--sync-about-links
       "Source"
       '(("Target" (passage ("Source" 0 42)) (transclusion-of "Transcribed")))
       'derives-from)
      (should captured)
      (should (equal (nth 0 captured) "arxana/transclusion"))
      (should (equal (nth 1 captured) ":hx/transclusion"))
      (let ((endpoints (nth 2 captured)))
        (should (= (length endpoints) 4))
        (should (equal (assoc 'role (car endpoints)) '(role . ":role/source")))
        (should (equal (assoc 'role (cadr endpoints)) '(role . ":role/target")))
        (should (equal (assoc 'role (caddr endpoints)) '(role . ":role/passage")))
        (should (equal (assoc 'role (cadddr endpoints)) '(role . ":role/transclusion"))))
      (let ((props (nth 3 captured)))
        (should (string= (cdr (assoc 'label props))
                         "(passage (\"Source\" 0 42)) | (transclusion-of \"Transcribed\")"))
        (should (equal (cdr (assoc 'scholium/type props)) "derives-from")))
      (should-not relation-called))))

(ert-deftest arxana-adjacency-falls-back-to-relations ()
  (let ((futon4-enable-sync t)
        (relation-calls nil)
        (hyperedge-called nil))
    (cl-letf (((symbol-function 'futon4--article-id-for)
               (lambda (name &optional _path)
                 (pcase name
                   ("Source" "id-source")
                   ("Target" "id-target"))))
              ((symbol-function 'futon4-lookup-article-id)
               (lambda (name)
                 (when (string= name "Source") "id-source")))
              ((symbol-function 'futon4-store-hyperedge)
               (lambda (&rest _)
                 (setq hyperedge-called t)))
              ((symbol-function 'futon4-store-nema-simple)
               (lambda (src dst label &optional _cb)
                 (push (list src dst label) relation-calls))))
      (futon4--sync-about-links "Source" '(("Target")) 'reference)
      (should (equal relation-calls '(("id-source" "id-target" ""))))
      (should-not hyperedge-called))))

(ert-deftest arxana-adjacency-skips-missing-targets ()
  (let ((futon4-enable-sync t)
        (relation-called nil)
        (hyperedge-called nil))
    (cl-letf (((symbol-function 'futon4--article-id-for)
               (lambda (name &optional _path)
                 (when (string= name "Source") "id-source")))
              ((symbol-function 'futon4-lookup-article-id)
               (lambda (name)
                 (when (string= name "Source") "id-source")))
              ((symbol-function 'futon4-store-hyperedge)
               (lambda (&rest _)
                 (setq hyperedge-called t)))
              ((symbol-function 'futon4-store-nema-simple)
               (lambda (&rest _)
                 (setq relation-called t))))
      (futon4--sync-about-links "Source" '(("Unknown" (passage 0 42))) 'derives-from)
      (should-not relation-called)
      (should-not hyperedge-called))))

(provide 'arxana-adjacency-test)
;;; arxana-adjacency-test.el ends here
